## people-clojure

Now that we have parsed the CSV file, let's modify our project to display it on a webpage. To do so, we're going to need to add a few libraries to our `project.clj` file. Open it up and you'll see something like this:

```clojure
(defproject people-clojure "0.0.1-SNAPSHOT"
  :description "FIXME: write description"
  :dependencies [[org.clojure/clojure "1.8.0"]]
  :javac-options ["-target" "1.6" "-source" "1.6" "-Xlint:-options"]
  :aot [people-clojure.core]
  :main people-clojure.core)
```

This file lists some metadata about the project. The most important part is `:dependencies`, which lists what libraries we are using. Currently, we are just using Clojure itself. Let's add several other libraries like this:

```clojure
(defproject people-clojure "0.0.1-SNAPSHOT"
  :description "FIXME: write description"
  :dependencies [[org.clojure/clojure "1.8.0"]
                 [ring "1.4.0"]
                 [hiccup "1.0.5"]
                 [compojure "1.5.0"]]
  :javac-options ["-target" "1.6" "-source" "1.6" "-Xlint:-options"]
  :aot [people-clojure.core]
  :main people-clojure.core)
```

Save the file. If you are currently running anything, click "Stop", because it won't retrieve the new libraries if it is still running. Now let's start a new REPL by hitting "Run with REPL".

Our first goal is to remove some of the code at the end of the `-main` function so, instead of the user typing a country name and writing the results to a file, we simply return all the parsed data so we can display it on a page. The resulting main function should look like this:

```clojure
(defn -main []
  (let [people (slurp "people.csv")
        people (str/split-lines people)
        people (map (fn [line]
                      (str/split line #","))
                 people)
        header (first people)
        people (rest people)
        people (map (fn [line]
                      (zipmap header line))
                 people)]
    people))
```

Now letâ€™s move it out of the main function so we can free it up for all of our web server code. The easiest way to do that is to simply rename `-main` to something else, such as `read-people`:

```clojure
(ns people-clojure.core
  (:require [clojure.string :as str])
  (:gen-class))

(defn read-people []
  ...)
```

Next, we need to add our web server code. We'll be using a library called compojure to define a GET route. We need to add it into our `:require` list, which is kind of like an import statement in Java. We'll alias the library with the letter `c` to make it easier to refer to later:

```clojure
(ns people-clojure.core
  (:require [clojure.string :as str]
            [compojure.core :as c])
  (:gen-class))

...
```

Now we can define our first route. The syntax for this involves using `defroutes` and creating a GET route inside of it:

```clojure
...

(c/defroutes app
  (c/GET "/" []
    "Hello, world!"))
```

The next thing we need to do is start the web server. We'll use Jetty, which you can add to the `:require` list like this:

```clojure
(ns people-clojure.core
  (:require [clojure.string :as str]
            [compojure.core :as c]
            [ring.adapter.jetty :as j])
  (:gen-class))

...
```

The most logical place to start the server is in the main function, so we will recreate that. We simply need to start the server and pass it the `app` var so it knows what routes to create.

```clojure
...

(defn -main []
  (j/run-jetty app {:port 3000 :join? false}))
```

Save, reload, and run `(-main)` in the REPL. You'll see some output printed, and if you go to `http://localhost:3000` you should see "Hello, world!". The hash map provides the options to the web server. Note that `:join? false` tells it to run the server in the background so we can continue to use the REPL.

Notice that when you try running `(-main)` again you get an error: "Address already in use". This is because the web server from before is still running. What we need to do is save that web server into a variable, and tell it to stop before running the new one.

Clojure tries to avoid "mutating" values like this, but sometimes it is necessary. The simplest mechanism to do so is to create an `atom`. This is like a little container that lets you "reset" its contents whenever you want. Let's define one called `server`. We are using `defonce` so that it isn't overwritten when we reload our code each subsequent time:

```clojure
...

(defonce server (atom nil))

(defn -main []
  (j/run-jetty app {:port 3000 :join? false}))
```

Now, we must capture the return value of the `run-jetty` function, which returns the server object. We can do this by calling `reset!` on the atom and storing the server object inside of it:

```clojure
...

(defn -main []
  (reset! server (j/run-jetty app {:port 3000 :join? false})))
```

Finally, before that line, we should stop the existing server if there is one stored in the `server` var. We can pull the value out of it by putting a `@` in front of it. Then we can call a Java method called `stop`, which is listed in the [API docs](http://download.eclipse.org/jetty/9.2.16.v20160414/apidocs/org/eclipse/jetty/server/Server.html). In Clojure, you can call Java methods using a special interop syntax:

```clojure
(defn -main []
  (when @server
    (.stop @server))
  (reset! server (j/run-jetty app {:port 3000 :join? false})))
```

Now click the "Stop" button and then hit "Run with REPL" again. You should now be able to run `(-main)` as many times as you want, because it is stopping the old server before running the new one. Try changing the route so it returns "Hello!". Save, reload, and run `(-main)` again to restart the server, and in your browser you should see the new text. This is an example of doing server-side code with REPL-driven development!

Let's continue building our web page. We need to generate HTML on the server and send it down the GET route. The most common way of doing this in Clojure is with a library called hiccup, which lets you write Clojure data structures and convert them into HTML. Add it to the `:require` list:

```clojure
(ns people-clojure.core
  (:require [clojure.string :as str]
            [compojure.core :as c]
            [ring.adapter.jetty :as j]
            [hiccup.core :as h])
  (:gen-class))
```

Then use its `html` function to generate the HTML. It expects you to write a series of vectors with the tag name in the beginning, like this:

```clojure
...

(c/defroutes app
  (c/GET "/" []
    (h/html [:html
             [:body
              [:h2 "Hello, world!"]]])))

...
```

Save, reload, and run `(-main)` in the REPL again. Refresh the page in your browser and you should now see a the text in a nice large font! If you look at the page source, you can see what hiccup generated for us:

```html
<html><body><h2>Hello, world!</h2></body></html>
```

