## tic-tac-toe-clojure

We'll begin with a small exercise. Let's write a function that takes a two-dimensional vector representing a tic-tac-toe board, and returns whether either side has won. In Nightcode, create a console project called `tic-tac-toe-clojure`. In core.clj, start by defining the board:

```clojure
(ns tic-tac-toe-clojure.core
  (:gen-class))

(def board [[:x :e :o]
            [:x :e :e]
            [:x :e :o]])

(defn -main []
  (println "Hello, World!"))
```

The `:e` spots are empty. To solve this, we need to pull all nine values out of the `board` var. We could do this manually, but a faster solution is to use destructuring. Let's start with a simple example of destructuring. Imagine you created a vector of names...

```clojure
(def names ["Alice" "Bob" "Charlie"])
```

If you wanted to pull them all out and print them, you could use `get` to retrieve them one at a time like this:

```clojure
(let [alice (get names 0)
      bob (get names 1)
      charlie (get names 2)]
  (println alice bob charlie))
```

Or, you could use destructuring to pull them all out at once:

```clojure
(let [[alice bob charlie] names]
  (println alice bob charlie))
```

This is a pretty powerful facility, and it works on multi-dimensional data structures like the tic-tac-toe board. Going back to our project, we can pull out each individual value from the board like this:

```clojure
...

(defn -main []
  (let [[[a b c]
         [d e f]
         [g h i]] board]
    (println a b c d e f g h i)))
```

Click "Run with REPL". When the prompt appears, run `(-main)` and confirm that you can see all nine spots printed out. The next question is, how do we determine if someone won? There are exactly eight possible ways to win. There are three horizontal lines, three vertical lines, and two diagonal lines.

So, what we need to do is create a vector for each possible line, and check to see if any of them are `[:x :x :x]` or `[:o :o :o]`. What should we store the possibilities in? Since we are essentially going to ask whether it *contains* a value, the most logical answer would be a hash set.

Hash sets are very fast at checking whether they contain a value. For example, if you made a hash set of names like this:

```clojure
(def names (hash-set "Alice" "Bob" "Charlie"))
```

You could easily check if it contains a certain name like this:

```clojure
(contains? names "Bob")
```

Hash sets are much faster at checking their contents than vectors, so it would make a lot of sense to use them here. Let's make a local var called `solutions` that contains all eight possibilities:

```clojure
...

(defn -main []
  (let [[[a b c]
         [d e f]
         [g h i]] board
        solutions (hash-set
                    [a b c]
                    [d e f]
                    [g h i]
                    [a d g]
                    [b e h]
                    [c f i]
                    [a e i]
                    [c e g])]
    solutions))
```

Save, reload, and run `(-main)` in the REPL again. You should get the following, which is a hash set containing the vectors you specified:

```clojure
#{[:x :e :o] [:o :e :x] [:e :e :e] [:x :e :e] [:x :x :x] [:o :e :o]}
```

Now, all we need to do is check if it contains a win. If neither side won, we should return `nil`. In another language, we might use `if` and `else if` statements. The equivalent in Clojure is called `cond`, and it looks like this:

```clojure
...

(defn -main []
  (let [[[a b c]
         [d e f]
         [g h i]] board
        solutions (hash-set
                    [a b c]
                    [d e f]
                    [g h i]
                    [a d g]
                    [b e h]
                    [c f i]
                    [a e i]
                    [c e g])]
    (cond
      (contains? solutions [:x :x :x]) :x
      (contains? solutions [:o :o :o]) :o
      :else nil)))
```

Save, reload, and run `(-main)` in the REPL again. It should now return `:x`, which means the first branch, `(contains? solutions [:x :x :x])`, was true, causing `cond` to return the value next to it. You can try modifying the `board` to make sure it works for other arrangements.

## microblog-clojure

Create a new console project called `microblog-clojure`. In your project.clj, add the three libraries we added yesterday:

```clojure
(defproject microblog-clojure "0.0.1-SNAPSHOT"
  :description "FIXME: write description"
  :dependencies [[org.clojure/clojure "1.8.0"]
                 [ring "1.4.0"]
                 [hiccup "1.0.5"]
                 [compojure "1.5.0"]]
  :javac-options ["-target" "1.6" "-source" "1.6" "-Xlint:-options"]
  :aot [microblog-clojure.core]
  :main microblog-clojure.core)
```

Save it, then open core.clj. We'll start by just setting it up to show "Hello, world!" at the main `/` route. We'll also use the same technique we used before to make the server restart every time we run the main function:

```clojure
(ns microblog-clojure.core
  (:require [compojure.core :as c]
            [ring.adapter.jetty :as j])
  (:gen-class))

(defonce server (atom nil))

(c/defroutes app
  (c/GET "/" []
    "Hello, world!"))

(defn -main []
  (when @server
    (.stop @server))
  (reset! server (j/run-jetty app {:port 3000 :join? false})))
```

This app will provide a simple form to let you post a message, and the page will refresh to display the past messages. We need to start by creating an `atom` to store them:

```clojure
(ns microblog-clojure.core
  (:require [compojure.core :as c]
            [ring.adapter.jetty :as j])
  (:gen-class))

(defonce server (atom nil))
(defonce messages (atom []))

...
```

It starts off as an empty vector. The first step is to use hiccup to return a form that we can use to submit our messages. First, add it to the `:require` list:

```clojure
(ns microblog-clojure.core
  (:require [compojure.core :as c]
            [ring.adapter.jetty :as j]
            [hiccup.core :as h])
  (:gen-class))

...
```

Then return a simple page with the form:

```clojure
...

(c/defroutes app
  (c/GET "/" []
    (h/html [:html
             [:body
              [:form {:action "/add-message" :method "post"}
               [:input {:type "text" :placeholder "Enter message" :name "message"}]
               [:button {:type "submit"} "Add Message"]]]])))

...
```

Save, reload, and run `(-main)` in the REPL. Verify that you see the page at `http://localhost:3000`.

Now we need to make a post route that accepts a `message` query parameter and saves it to the atom. First, we need to add a few additional ring namespaces that we'll need to use:

```clojure
(ns microblog-clojure.core
  (:require [compojure.core :as c]
            [ring.adapter.jetty :as j]
            [hiccup.core :as h]
            [ring.middleware.params :as p]
            [ring.util.response :as r])
  (:gen-class))

...
```

First, we need to wrap our `app` object with a special function that makes it parse the query parameters as URL-encoded parameters. We can do so like this:

```clojure
...

(defn -main []
  (when @server
    (.stop @server))
  (let [app (p/wrap-params app)]
    (reset! server (j/run-jetty app {:port 3000 :join? false}))))
```

Then, we need to create the POST route, which will pull the parameters out of the request map, pull the message parameter out of that, add it to the atom, and then redirect back to the `/` route:

```clojure
...

(c/defroutes app
  (c/GET "/" []
    ...)
  (c/POST "/add-message" request
    (let [params (:params request)
          message (get params "message")]
      (swap! messages conj message)
      (r/redirect "/"))))

...
```

Save, reload, and run `(-main)` in the REPL again. Refresh the page and write "Hello world!". After clicking the button, go back to the REPL and enter `@messages`. You should see this:

```clojure
["Hello world!"]
```

Now that we are receiving the message, let's modify the GET route to list them:

```clojure
...

(c/defroutes app
  (c/GET "/" []
    (h/html [:html
             [:body
              [:form {:action "/add-message" :method "post"}
               [:input {:type "text" :placeholder "Enter message" :name "message"}]
               [:button {:type "submit"} "Add Message"]]
              [:ol
               (map (fn [message]
                      [:li message])
                 @messages)]]]))
  (c/POST "/add-message" request
    ...))

...
```
